@echo off

@rem ----------------------------------------------------------------
@rem ffmpegで「m4a → mp3」変換
@rem ----------------------------------------------------------------
@rem -ac 1:モノラル／2:ステレオ
@rem -ab ビットレート
@rem -ar サンプリングレート
@rem ----------------------------------------------------------------
@rem forfiles /s /m *.m4a /c "cmd /c ffmpeg -i @path -vn -acodec mp3 -ac 2 -ab 256 -ar 44100 -f mp3 @path.mp3"
forfiles /s /m *.m4a /c "cmd /c ffmpeg -i @path -vn -acodec mp3 -ac 2 -f mp3 @path.mp3"

@rem ----------------------------------------------------------------
@rem for 文内で変数を使うので「遅延環境変数」を有効化
@rem ----------------------------------------------------------------
setlocal ENABLEDELAYEDEXPANSION

@rem ----------------------------------------------------------------
@rem ファイル名変更のループ
@rem ----------------------------------------------------------------
@rem ファイル名の末尾を「～.m4a.mp3」にしちゃったので、
@rem 「～.mp3」へ変更するためのループ処理
@rem ----------------------------------------------------------------
for /r %%i in (*.m4a.mp3) do (
set m4a=%%i
set mp3=!m4a:~0,-8!.mp3
call :RENAME_CMD "!mp3!"
)

@rem ----------------------------------------------------------------
@rem 「setlocal」の設定を無効化
@rem ----------------------------------------------------------------
endlocal

@rem ----------------------------------------------------------------
@rem 不要なら、変換元「～.m4a」ファイルを削除
@rem ----------------------------------------------------------------
@rem 変換元を削除しないなら、ここをコメントアウトしてね
@rem ----------------------------------------------------------------
for /r %%i in (*.m4a) do (
del "%%i"
)

pause
goto :END

:RENAME_CMD
@rem ----------------------------------------------------------------
@rem ファイル名変更の本体
@rem ----------------------------------------------------------------
ren "!m4a!" "%~nx1"
goto :EOF

:END
